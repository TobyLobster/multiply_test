; smult10.a
; from Piotr Fusik in the short lived Polish Atari Disk Magazine Syzygy 6 http://ftp.pigwa.net/stuff/collections/atari_forever/Ziny/Syzygy/ (1999)
; (for English version see https://forums.atariage.com/topic/78839-russian-multiplication/#comment-975865)
;
; 8 bit x 8 bit signed multiply, 16 bit result
; Average cycles: 53.99
; 2083 bytes


* = 0x200

; Tables must be aligned to page boundary
square1_low
    !for i, -256, 255 {
        !byte <((i*i)/4)
    }
square1_high
    !for i, -256, 255 {
        !byte >((i*i)/4)
    }

square2_low
    !for i, -255, 256 {
        !byte <((i*i)/4)
    }
square2_high
    !for i, -255, 256 {
        !byte >((i*i)/4)
    }

; ----------------------------------------------------------------------------------
; Description: Signed 8-bit multiplication with signed 16-bit result.
;
; On Entry:
;   A: 8-bit signed value
;   X: 8-bit signed value
;
; On Exit:
;   Y (low byte) and A (high byte): 16-bit signed product
mult_8bit_signed
    eor #$80
    sta sm1
    sta sm3
    eor #$ff
    sta sm2
    sta sm4
    txa
    eor #$80
    tax

    sec
sm1 = * + 1
    lda square1_low,x
sm2 = * + 1
    sbc square2_low,x
    tay
sm3 = * + 1
    lda square1_high,x
sm4 = * + 1
    sbc square2_high,x
    rts
