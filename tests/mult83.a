; mult83.a
; from Retro Software: http://www.retrosoftware.co.uk/wiki/index.php?title=Fast_multiplication_routines
;
; 8 bit x 8 bit unsigned multiply, 16 bit result
; Average cycles: 56.00
; 1079 bytes

num1 = $02
num2 = $03
result = $04    ; 2 bytes

* = $0200

; Align tables to start of page for speed
sqrlo256
    !for i, 0, 255 {
        !byte <((i*i)/4)
    }
sqrhi256
    !for i, 0, 255 {
        !byte >((i*i)/4)
    }
sqrlo512
    !for i, 0, 255 {
        !byte <(((i+256)*(i+256))/4)
    }
sqrhi512
    !for i, 0, 255 {
        !byte >(((i+256)*(i+256))/4)
    }

mult
    sec
    lda num1
    sbc num2
    bcs positive
    eor #255
    adc #1
positive
    tay
    clc
    lda num1
    adc num2
    tax
    bcs morethan256
    sec
    lda sqrlo256,X
    sbc sqrlo256,Y
    sta result
    lda sqrhi256,X
    sbc sqrhi256,Y
    sta result+1
    rts

morethan256
    lda sqrlo512,X
    sbc sqrlo256,Y
    sta result
    lda sqrhi512,X
    sbc sqrhi256,Y
    sta result+1
    rts
