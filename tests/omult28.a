; omult28.a
; from Starship Command at $1095: http://www.level7.org.uk/miscellany/starship-command-disassembly.txt
;
; 24 bit x 8 bit unsigned multiply, 24 bit result (high bytes)
; Average cycles: 897.00
; 24 bytes

* = $0200

multiplier           = $02
input_fraction       = $03
input_pixels         = $04
input_screens        = $05
sine_output_fraction = $06
sine_output_pixels   = $07

; 24 bit x 8 bit unsigned multiply, 24 bit result (low bytes)
;
; On Entry:
;   (input_fraction, input_pixels, input_screens): 24 bit input
;   multiplier: 8 bit input
; On Exit:
;   (sine_output_fraction, sine_output_pixels, A): 24 bit result
mult
    lda #0
    ldy #24                 ; 24-bit multiplication of input by multiplier
loop_over_bits_of_input
    lsr input_screens
    ror input_pixels
    ror input_fraction      ; Get lowest bit of (fraction, pixels, screen)
    bcc input_bit_unset
    clc
    adc multiplier
input_bit_unset
    ror
    ror sine_output_pixels
    ror sine_output_fraction
    dey
    bne loop_over_bits_of_input
    rts
