; mult57.a
; from https://sites.google.com/site/h2obsession/programming/6502
; alternative smaller memory version (see mult27.a)
;
; 8 bit x 8 bit unsigned multiply, 16 bit result
; Average cycles: 48.49
; 1058 bytes


* = $0200

prod_low = $02

; Tables must be aligned with page boundary
sqrLow
    !for i, 0, 511 {
        !byte <((i*i)/4)
    }

sqrHigh
    !for i, 0, 511 {
        !byte >((i*i)/4)
    }

; ***************************************************************************************
; On Entry:
;   A = multiplicand
;   X = multiplier
; On Exit:
;   prod_low: low byte of product
;   A:        high byte of product
mult
    sta getLow+1
    sta getHigh+1

    stx temp1
    sec
temp1 = *+1
    sbc #0
    bcs doMult
    sbc #0
    eor #255
doMult
    tay
getLow
    lda sqrLow,x
    sbc sqrLow,y
    sta prod_low
getHigh
    lda sqrHigh,x
    sbc sqrHigh,y
    rts
