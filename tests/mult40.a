; mult40.a
; from https://gitlab.riscosopen.org/RiscOS/Sources/Apps/Diversions/Meteors/-/blob/master/Srce6502/MetSrc
; from 'Meteors' for the BBC Micro
;
; 8 bit x 8bit unsigned multiply, 16 bit result
; Average cycles: 278
; 35 bytes


_a = $02        ; two bytes
_x = $04
_y = $05        ; two bytes

* = $0200

; ***************************************************************************************
; On Entry:
;   _x: multiplier
;   _y: multiplicand
; On Exit:
;   _a:   low byte of product
;   _a+1: high byte of product
mult
    lda #0
    sta _y+1
    sta _a
    sta _a+1
    ldy #8
mloop
    lsr _x
    bcc noadd
    clc
    lda _a
    adc _y
    sta _a
    lda _a+1
    adc _y+1
    sta _a+1
noadd
    asl _y
    rol _y+1
    dey
    bne mloop
    rts
